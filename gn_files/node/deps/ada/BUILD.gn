# Copyright 2023 Microsoft Inc.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//node/deps/v8/gni/v8.gni")

config("ada_config") {
  include_dirs = [ "." ]
}

gypi_values = exec_script("//gn_tools/gypi_to_gn.py",
                          [ rebase_path("ada.gyp") ],
                          "scope",
                          [ "ada.gyp" ])

source_set("ada") {
  public_configs = [ ":ada_config" ]
  sources = gypi_values.ada_sources
  if (v8_enable_i18n_support) {
    deps = [
      "//third_party/icu:icui18n",
      "//third_party/icu:icuuc",
    ]
    if (is_win) {
      deps += [ "//third_party/icu:icudata" ]
    }
  } else {
    defines = [ "ADA_HAS_ICU=0" ]
  }
}
