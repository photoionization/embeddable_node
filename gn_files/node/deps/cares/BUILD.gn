# Copyright (c) 2013-2019 GitHub Inc.
# Copyright 2019 the V8 project authors. All rights reserved.
# Copyright 2023 Microsoft Inc.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//node/node_files.gni")

config("cares_config") {
  include_dirs = [ "include" ]
  if (!is_component_build) {
    defines = [ "CARES_STATICLIB" ]
  }
}

component("cares") {
  public_configs = [ ":cares_config" ]
  if (is_component_build) {
    defines = [ "CARES_BUILDING_LIBRARY" ]
  } else {
    defines = []
  }
  if (is_win) {
    defines += [ "CARES_PULL_WS2TCPIP_H=1" ]
  } else {
    defines += [
      "_DARWIN_USE_64_BIT_INODE=1",
      "_LARGEFILE_SOURCE",
      "_FILE_OFFSET_BITS=64",
      "_GNU_SOURCE",
      "HAVE_CONFIG_H",
    ]
  }

  include_dirs = [ "src/lib" ]
  if (is_win) {
    include_dirs += [ "config/win32" ]
  } else if (is_linux) {
    include_dirs += [ "config/linux" ]
  } else if (is_mac) {
    include_dirs += [ "config/darwin" ]
  }

  if (is_win) {
    libs = [
      "ws2_32.lib",
      "iphlpapi.lib",
    ]
  }

  sources = node_files.cares_sources
  if (is_win) {
    sources += [
      "src/lib/ares_getenv.c",
      "src/lib/ares_iphlpapi.h",
      "src/lib/ares_platform.c",
      "src/lib/config-win32.h",
      "src/lib/windows_port.c",
    ]
  }
  if (is_linux) {
    sources += [ "config/linux/ares_config.h" ]
  }
  if (is_mac) {
    sources += [ "config/darwin/ares_config.h" ]
  }
}
