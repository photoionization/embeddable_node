# Copyright (c) 2013-2019 GitHub Inc.
# Copyright 2019 the V8 project authors. All rights reserved.
# Copyright 2023 Microsoft Inc.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("cares_config") {
  include_dirs = [ "include" ]
  if (!is_component_build) {
    defines = [ "CARES_STATICLIB" ]
  }
}

gypi_values = exec_script("//gn_tools/gypi_to_gn.py",
                          [ rebase_path("cares.gyp") ],
                          "scope",
                          [ "cares.gyp" ])

component("cares") {
  public_configs = [ ":cares_config" ]
  if (is_component_build) {
    defines = [ "CARES_BUILDING_LIBRARY" ]
  } else {
    defines = []
  }
  if (is_win) {
    defines += [ "CARES_PULL_WS2TCPIP_H=1" ]
  }
  if (is_posix) {
    defines += [
      "_DARWIN_USE_64_BIT_INODE=1",
      "_LARGEFILE_SOURCE",
      "_FILE_OFFSET_BITS=64",
      "_GNU_SOURCE",
      "HAVE_CONFIG_H",
    ]
  }

  include_dirs = [ "src/lib" ]
  if (is_win) {
    include_dirs += [ "config/win32" ]
  } else if (is_linux) {
    include_dirs += [ "config/linux" ]
  } else if (is_mac) {
    include_dirs += [ "config/darwin" ]
  }

  if (is_win) {
    libs = [
      "ws2_32.lib",
      "iphlpapi.lib",
    ]
  }

  sources = gypi_values.cares_sources_common
  if (is_win) {
    sources += gypi_values.cares_sources_win
  }
  if (is_linux) {
    sources += [ "config/linux/ares_config.h" ]
  }
  if (is_mac) {
    sources += [ "config/darwin/ares_config.h" ]
  }
}
